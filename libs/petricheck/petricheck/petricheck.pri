PETRICHECK_VERSION = 0.1.0

isEmpty(PETRICHECK_LIBRARY_TYPE) {
    PETRICHECK_LIBRARY_TYPE = staticlib
}

PETRICHECK_PATH = $${PWD}
PETRICHECK_INCLUDEPATH = $${PETRICHECK_PATH}
PETRICHECK_LIBDIR = $$shadowed($$PWD)

CONFIG(debug, debug|release) {
    win*:PETRICHECK_LIBDIR = $$PETRICHECK_LIBDIR/debug
}else{
    win*:PETRICHECK_LIBDIR = $$PETRICHECK_LIBDIR/release
}

contains(PETRICHECK_LIBRARY_TYPE, staticlib) {
    DEFINES += PETRICHECK_LIBRARY_STATIC
} else {
    DEFINES += PETRICHECK_LIBRARY
}

INCLUDEPATH += $${PETRICHECK_INCLUDEPATH}
DEPENDPATH += $${PETRICHECK_INCLUDEPATH}
QMAKE_LIBDIR += $${PETRICHECK_LIBDIR}

CONFIG(debug, debug|release) {
    PETRICHECK_NAME = petricheckd
}else{
    PETRICHECK_NAME = petricheck
}

LIBS += -l$$PETRICHECK_NAME
DEFINES -= QT_NO_CAST_TO_ASCII QT_NO_CAST_FROM_ASCII

win*-msvc*{
    DEFINES += _SCL_SECURE_NO_WARNINGS
    DEFINES += _CRT_SECURE_NO_WARNINGS
}

isEmpty(MOBATA_LIB_PATH){
  MOBATA_LIB_PATH = $$PWD/../../../mobata
}
MOBATA_PRIFILE = $${MOBATA_LIB_PATH}/mobata.pri
requires(exists($${MOBATA_PRIFILE}))
include($${MOBATA_PRIFILE})
