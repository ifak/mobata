DISTESTRUNTIME_VERSION = 0.0.1


isEmpty(DISTESTRUNTIME_LIBRARY_TYPE) {
    DISTESTRUNTIME_LIBRARY_TYPE = staticlib
}

QT += core qml

DEFINES -= QT_NO_CAST_TO_ASCII QT_NO_CAST_FROM_ASCII

DISTESTRUNTIME_PATH = $${PWD}
DISTESTRUNTIME_INCLUDEPATH = $${DISTESTRUNTIME_PATH}/../

DISTESTRUNTIME_LIBDIR = $$shadowed($$PWD)
CONFIG(debug, debug|release) {
  win*:DISTESTRUNTIME_LIBDIR = $$DISTESTRUNTIME_LIBDIR/debug
}else{
  win*:DISTESTRUNTIME_LIBDIR = $$DISTESTRUNTIME_LIBDIR/release
}

contains(DISTESTRUNTIME_LIBRARY_TYPE, staticlib) {
    DEFINES += DISTESTRUNTIME_LIBRARY_STATIC
} else {
    DEFINES += DISTESTRUNTIME_LIBRARY
}

INCLUDEPATH += $${DISTESTRUNTIME_INCLUDEPATH}
DEPENDPATH += $${DISTESTRUNTIME_INCLUDEPATH}
QMAKE_LIBDIR += $${DISTESTRUNTIME_LIBDIR}

CONFIG(debug, debug|release) {
  DISTESTRUNTIME_NAME = distestruntimed
}else{
  DISTESTRUNTIME_NAME = distestruntime
}

LIBS += -l$$DISTESTRUNTIME_NAME

isEmpty(MOBATA_LIB_PATH){
  MOBATA_LIB_PATH = $$PWD/../../../mobata
}
MOBATA_PRIFILE = $${MOBATA_LIB_PATH}/mobata.pri
requires(exists($${MOBATA_PRIFILE}))
include($${MOBATA_PRIFILE})
