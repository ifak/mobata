MOBATAWIDGETS_VERSION = 0.8.0

isEmpty(MOBATAWIDGETS_LIBRARY_TYPE) {
    MOBATAWIDGETS_LIBRARY_TYPE = staticlib
}

QT += widgets network svg

MOBATAWIDGETS_PATH = $${PWD}
MOBATAWIDGETS_INCLUDEPATH = $${MOBATAWIDGETS_PATH}
MOBATAWIDGETS_LIBDIR = $$shadowed($$PWD/mobatawidgets)

CONFIG(debug, debug|release) {
  win*:MOBATAWIDGETS_LIBDIR = $$MOBATAWIDGETS_LIBDIR/debug
}else{
  win*:MOBATAWIDGETS_LIBDIR = $$MOBATAWIDGETS_LIBDIR/release
}

contains(MOBATAWIDGETS_LIBRARY_TYPE, staticlib) {
    DEFINES += MOBATAWIDGETS_LIBRARY_STATIC
} else {
    DEFINES += MOBATAWIDGETS_LIBRARY
}

INCLUDEPATH += $${MOBATAWIDGETS_INCLUDEPATH}
DEPENDPATH += $${MOBATAWIDGETS_INCLUDEPATH}
QMAKE_LIBDIR += $${MOBATAWIDGETS_LIBDIR}

CONFIG(debug, debug|release) {
  MOBATAWIDGETS_NAME = mobatawidgetsd
}else{
  MOBATAWIDGETS_NAME = mobatawidgets
}

LIBS += -l$$MOBATAWIDGETS_NAME

DEFINES -= QT_NO_CAST_TO_ASCII QT_NO_CAST_FROM_ASCII

win*-msvc*{
DEFINES += _SCL_SECURE_NO_WARNINGS
DEFINES += _CRT_SECURE_NO_WARNINGS
}

isEmpty(MOBATA_LIB_PATH){
  MOBATA_LIB_PATH = $$PWD/../../mobata
}
MOBATA_PRIFILE = $${MOBATA_LIB_PATH}/mobata.pri
requires(exists($${MOBATA_PRIFILE}))
include($${MOBATA_PRIFILE})
