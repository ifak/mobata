TRAFFICSIM_VERSION = 0.0.1


isEmpty(TRAFFICSIM_LIBRARY_TYPE) {
    TRAFFICSIM_LIBRARY_TYPE = staticlib
}

QT += core

DEFINES -= QT_NO_CAST_TO_ASCII QT_NO_CAST_FROM_ASCII

TRAFFICSIM_PATH = $${PWD}
TRAFFICSIM_INCLUDEPATH = $${TRAFFICSIM_PATH}/../

TRAFFICSIM_LIBDIR = $$shadowed($$PWD)
CONFIG(debug, debug|release) {
  win*:TRAFFICSIM_LIBDIR = $$TRAFFICSIM_LIBDIR/debug
}else{
  win*:TRAFFICSIM_LIBDIR = $$TRAFFICSIM_LIBDIR/release
}

contains(TRAFFICSIM_LIBRARY_TYPE, staticlib) {
    DEFINES += TRAFFICSIM_LIBRARY_STATIC
} else {
    DEFINES += TRAFFICSIM_LIBRARY
}

INCLUDEPATH += $${TRAFFICSIM_INCLUDEPATH}
DEPENDPATH += $${TRAFFICSIM_INCLUDEPATH}
QMAKE_LIBDIR += $${TRAFFICSIM_LIBDIR}

CONFIG(debug, debug|release) {
  TRAFFICSIM_NAME = trafficsimd
}else{
  TRAFFICSIM_NAME = trafficsim
}

LIBS += -l$$TRAFFICSIM_NAME

isEmpty(DSLPARSER_LIB_PATH){
  DSLPARSER_LIB_PATH = $$PWD/../../dslparser/dslparser
}
DSLPARSER_PRIFILE = $${DSLPARSER_LIB_PATH}/dslparser.pri
requires(exists($${DSLPARSER_PRIFILE}))
include($${DSLPARSER_PRIFILE})
