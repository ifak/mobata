TESTEXECUTORLIB_VERSION = 1.0

isEmpty(TESTEXECUTOR_LIBRARY_TYPE) {
    TESTEXECUTOR_LIBRARY_TYPE = staticlib
}

QT += core
QT += gui
QT += concurrent
QT += widgets

DEFINES -= QT_NO_CAST_TO_ASCII QT_NO_CAST_FROM_ASCII



TESTEXECUTOR_PATH = $${PWD}
TESTEXECUTOR_INCLUDEPATH = $${TESTEXECUTOR_PATH}
TESTEXECUTOR_LIBDIR = $$shadowed($$PWD/testexecutor)

CONFIG(debug, debug|release) {
  win*:TESTEXECUTOR_LIBDIR = $$TESTEXECUTOR_LIBDIR/debug
}else{
  win*:TESTEXECUTOR_LIBDIR = $$TESTEXECUTOR_LIBDIR/release
}

contains(TESTEXECUTOR_LIBRARY_TYPE, staticlib) {
    DEFINES += TESTEXECUTOR_LIBRARY_STATIC
} else {
    DEFINES += TESTEXECUTOR_LIBRARY
}

INCLUDEPATH += $${TESTEXECUTOR_INCLUDEPATH}
DEPENDPATH += $${TESTEXECUTOR_INCLUDEPATH}
QMAKE_LIBDIR += $${TESTEXECUTOR_LIBDIR}

CONFIG(debug, debug|release) {
  TESTEXECUTOR_NAME = testexecutorLibd
}else{
  TESTEXECUTOR_NAME = testexecutorLib
}

LIBS += -l$$TESTEXECUTOR_NAME

isEmpty(MOBATA_LIB_PATH){
  MOBATA_LIB_PATH = $$PWD/../../mobata
}
MOBATA_PRIFILE = $${MOBATA_LIB_PATH}/mobata.pri
include($${MOBATA_PRIFILE})

VIBN_MQTT_PATH = $$PWD/../../3rd/vibn_mqtt

include($${VIBN_MQTT_PATH}/vibn_mqtt.pri)
